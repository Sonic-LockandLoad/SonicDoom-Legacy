class SonicAbilities : Weapon replaces Fist
{
	Default
	{
		Weapon.SelectionOrder 3700;
		Weapon.Kickback 10;
		Tag "Sonic the Hedgehog";
		+WEAPON.MELEEWEAPON
		+WEAPON.NOAUTOFIRE
	}
	States
	{
		Ready:
			TNT1 A 1 A_WeaponReady();
			Loop;
		Deselect:
			TNT1 A 1 A_Lower;
			Loop;
		Select:
			TNT1 A 1 A_Raise;
			Loop;
		Fire:
			PUNG B 1;
			PUNG C 1
			{
				if (CountInv("PowerSuper"))
				{
					A_CustomPunch(40, false, 0, "InvisiblePuff", 86, 0, 0, "ArmorBonus", "sweapons/finalhit");
				}
				else
				{
					A_CustomPunch(4, false, 0, "InvisiblePuff", 86, 0, 0, "ArmorBonus", "sweapons/finalhit");
				}
			}
			PUNG D 1;
			PUNG C 1;
			PUNG B 1 A_ReFire();
			Goto Ready;
		AltFire:
			TNT1 A 0
			{
				if (random[HereWeGo](1, 2) == 1)
				{
					A_PlaySound("classic/driftline", 7);
				}
			}
			// intentional passthrough
		Drift:
			TNT1 A 0 A_PlaySound("sweapons/drift", 2);
			TNT1 A 0
			{
				if (A_CheckFloor("Drift"))
				{
					A_Recoil(-12); // on the ground
				}
				else
				{
					A_Recoil(-6); // in the air
				}
			}
			TNT1 A 0 A_Light(2);
			SPIN A 1
			{
				if (CountInv("PowerSuper") > 0)
				{
					A_SetSpeed(8);
					A_CustomPunch(4, false, 0, "InvisiblePuff", 86, 0, 0, "ArmorBonus", "sweapons/finalhit");
				}
				else
				{
					A_SetSpeed(4);
					A_CustomPunch(2, false, 0, "InvisiblePuff", 64, 0, 0, "ArmorBonus", "sweapons/finalhit");
				}
			}
			TNT1 A 0 A_ReFire("Drift");
			TNT1 A 0 A_StopSound(2);
			TNT1 A 0 A_Light(0);
			Goto Ready;
	}
}

class ChaosBlaster : Weapon
{
	Default
	{
		Weapon.SelectionOrder 100;
		Weapon.AmmoUse 2;
		Weapon.AmmoGive 25;
		Weapon.KickBack 100;
		Weapon.BobSpeed 1.5;
		Weapon.BobRangeX 0.5;
		Weapon.AmmoType "ChaosEnergy";
		Tag "Chaos Blaster";
		Inventory.PickupMessage "Picked up the Chaos energy blaster!";
	}
	States
	{
		Ready:
			PLSG A 1 A_WeaponReady();
			Loop;
		Deselect:
			PLSG A 1 A_Lower;
			Loop;
		Select:
			PLSG A 1 A_Raise;
			Loop;
		Fire:
			PLSG A 0 A_FirePlasma();
			PLSG A 1
			{
				if (CountInv("PowerSuper") > 0)
				{
					A_GiveInventory("ChaosEnergy", 2);
				}
			}
			Goto Ready;
		Flash:
			PLSF A 4 Bright A_Light1;
			Goto LightDone;
			PLSF B 4 Bright A_Light1;
			Goto LightDone;
		Spawn:
			PLAS A -1;
			Stop;
	}
}
class SpeedyShotgun : Weapon
{
	Default
	{
		Weapon.SelectionOrder 1300;
		Weapon.AmmoUse 8;
		Weapon.AmmoGive 32;
		Weapon.KickBack 500;
		Weapon.AmmoType1 "ChaosEnergy";
		Weapon.AmmoType2 "ChaosEnergy";
		AttackSound "weapons/shotgf";
		Tag "Speedy Shotgun";
		Weapon.BobSpeed 1.5;
		Weapon.BobRangeX 0.5;
		Inventory.PickupMessage "Picked up the speedy shotgun!";
	}
	States
	{
		Ready:
			SHTG A 1 A_WeaponReady();
			Loop;
		Deselect:
			SHTG A 1 A_Lower;
			Loop;
		Select:
			SHTG A 1 A_Raise;
			Loop;
		Fire:
			SHTG A 4
			{
				A_FireBullets(5.6, 0, 7, 6, "BulletPuff");
				if (CountInv("PowerSuper")) {
					A_GiveInventory("ChaosEnergy", 8);
				}
				A_PlaySound("weapons/shotgf", CHAN_WEAPON);
				A_GunFlash();
			}
			Goto Ready;
		Flash:
			SHTF A 3 Bright A_Light1;
			SHTF B 2 Bright A_Light2;
			Goto LightDone;
		Spawn:
			SHOT A -1;
			Stop;
	}
}

class ClassicRocketStream : Weapon
{
	Default
	{
		Weapon.SelectionOrder 2500;
		Weapon.AmmoUse 30;
		Weapon.AmmoGive 50;
		Weapon.AmmoType "ChaosEnergy";
		Tag "Rocket Stream";
		Weapon.BobSpeed 1.5;
		Weapon.BobRangeX 0.5;
		Inventory.PickupMessage "Picked up the rocket stream launcher!";
	}
	States
	{
		Ready:
			MISG A 1 A_WeaponReady();
			Loop;
		Deselect:
			MISG A 1 A_Lower;
			Loop;
		Select:
			MISG A 1 A_Raise;
			Loop;
		Fire:
			MISG B 1 A_GunFlash();
			MISG B 0 A_FireProjectile("Rocket");
			MISG A 1
			{
				if (CountInv("PowerSuper") > 0)
				{
					A_GiveInventory("ChaosEnergy", 30);
				}
			} 
			Goto Ready;
		Flash:
			MISF A 3 Bright A_Light1;
			MISF B 4 Bright;
			MISF CD 4 Bright A_Light2;
			Goto LightDone;
		Spawn:
			LAUN A -1;
			Stop;
	}
}

class ChaosRain : Weapon
{
	Default
	{
		Weapon.SelectionOrder 700;
		Weapon.AmmoUse 1;
		Weapon.AmmoGive 20;
		Weapon.AmmoType "ChaosEnergy";
		Tag "Chaos Rain";
		Weapon.BobSpeed 1.5;
		Weapon.BobRangeX 0.5;
		Inventory.PickupMessage "Picked up the Chaos Rain cannon!";
	}
	States
	{
		Ready:
			CHGG A 1 A_WeaponReady();
			Loop;
		Deselect:
			CHGG A 1 A_Lower;
			Loop;
		Select:
			CHGG A 1 A_Raise;
			Loop;
		Fire:
			CHGG A 1
			{
				A_PlaySound("weapons/chngun", CHAN_WEAPON);
				A_GunFlash();
				A_FireBullets(5.6, 0, 1, 7, "BulletPuff");
				if (CountInv("PowerSuper") > 0) { A_GiveInventory("ChaosEnergy", 1); }
			}
			CHGG B 1
			{
				A_PlaySound("weapons/chngun", CHAN_WEAPON);
				A_GunFlash();
				A_FireBullets(5.6, 0, 1, 7, "BulletPuff");
				if (CountInv("PowerSuper") > 0) { A_GiveInventory("ChaosEnergy", 1); }
			}
			TNT1 A 0 A_ReFire();
			Goto Ready;
		Spawn:
			MGUN A -1;
			Stop;
	}
}
#library "snocdoom"
#include "zcommon.acs"
#define CSF_SOLIDGROUND 1
#define CSF_SOLIDACTORS 2
#define CSF_ALL (CSF_SOLIDGROUND|CSF_SOLIDACTORS)
/* Following script taken from the ZDoom Forums <https://forum.zdoom.org/viewtopic.php?t=51848#p903998> */

script "LastWeaponHandler" enter
{
    int pn            = PlayerNumber();
    str currentWeapon = GetWeapon();
    if (GetCvar("LastWeapon") == "") {
		SetCvar("lastWeapon", currentWeapon);
	}
    
    while (1) {
        if (StrCmp(currentWeapon, GetWeapon())) {
			SetCvar("lastWeapon", currentWeapon);
		}
        currentWeapon = GetWeapon();
        Delay(1);
    }
}

script "LastWeapon" (void)
{
    SetWeapon(GetCvar("lastWeapon"));
}

/* End of borrowed script */

script "Taunt" (void)
{
	NoiseAlert(0, 0);
	int tauntchance;
	tauntchance = random(1, 3);
	switch (tauntchance) {
		case 1:
			PlaySound(1001, "sonic/taunt1", 9, 1.0);
			if (!(CheckInventory("PowerSuper"))) {
				SetMugshotState("GrinSmol");
			}
			break;
		case 2:
			PlaySound(1001, "sonic/taunt2", 9, 1.0);
			if (!(CheckInventory("PowerSuper"))) {
				SetMugshotState("GrinSmol");
			}
			break;
		case 3:
			PlaySound(1001, "sonic/taunt3", 9, 1.0);
			if (!(CheckInventory("PowerSuper"))) {
				SetMugshotState("GrinSmol");
			}
			break;
	}
}

script "TransformSuper" (void)
{
	if (CheckInventory("PowerSuper"))
	{
		SetMusic("m_supsnc", 0);
		while (CheckInventory("PowerSuper")) {
		    SetMugShotState("Super");
			Delay(35);
		}
		SetMusic("*", 0);
		SetMugShotState("Normal");
	}
	else
	{
		SetMusic("*", 0);
	}
}

script "Invincibility" (void)
{
	if (!CheckInventory("PowerSuper")) {
		if (CheckInventory("PowerInvulnerable")) {
			SetMusic("s_invinc", 0);
			while (CheckInventory("PowerInvulnerable")) {
				delay(15);
			}
			SetMusic("*", 0);
		}
		else {
			SetMusic("*", 0);
		}
	}
}

script "InitMusic" ENTER
{
	SetMugShotState("Normal");
	SetMusic("*", 0);
}

script "TakeRingsIfSuper" (void)
{
	while (CheckInventory("PowerSuper"))
	{
		Delay(3);
		TakeInventory("ChaosEnergy", 1);
		if (!CheckInventory("ChaosEnergy"))
		{
			TakeInventory("PowerSuper", 1);
			TakeInventory("PowerInvulnerable", 1);
		}
	}
}

script "SetAirControl" ENTER
{
	 SetAirControl(0.5);
}

script "RingCountCheck" ENTER
{
	while (true)
	{
		Delay(2);
		if (CheckInventory("Health") > 500)
		{
			GiveInventory("OverRinged2", 1);
		}
		else if (CheckInventory("Health") > 300)
		{
			GiveInventory("OverRinged", 1);
		}
		else
		{
			TakeInventory("PowerOverRinged", 1);
			TakeInventory("PowerOverRinged2", 1);
		}
	}
	
}

script "GiveTID" ENTER
{
	Thing_ChangeTID(0, 1001);
}

script "NoMusic" (void)
{
	SetMusic("", 0);
}

script "ResetMusic" (void)
{
	SetMusic("*", 0);
}